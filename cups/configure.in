dnl
dnl "$Id$"
dnl
dnl   Configuration script for the CUPS drivers.
dnl
dnl   Copyright 1993-2000 by Easy Software Products.
dnl
dnl   This program is free software; you can redistribute it and/or
dnl   modify it under the terms of the GNU General Public License,
dnl   version 2, as published by the Free Software Foundation.
dnl
dnl   This program is distributed in the hope that it will be useful,
dnl   but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl   MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl   GNU General Public License for more details.
dnl
dnl   You should have received a copy of the GNU General Public License
dnl   along with this program; if not, please contact Easy Software
dnl   Products at:
dnl
dnl       Attn: CUPS Licensing Information
dnl       Easy Software Products
dnl       44141 Airport View Drive, Suite 204
dnl       Hollywood, Maryland 20636-3111 USA
dnl
dnl       Voice: (301) 373-9603
dnl       EMail: cups-info@cups.org
dnl         WWW: http://www.cups.org
dnl

dnl Make sure we have the template for the makefile...
AC_INIT(Makefile.in)
AC_PREFIX_DEFAULT(/usr)

dnl What OS?
uname=`uname`
if test "$uname" = "IRIX64"; then
    uname="IRIX"
fi

dnl Compiler options...
CFLAGS="${CFLAGS:=}"
OPTIM=""

AC_ARG_ENABLE(debug, [  --enable-debug        turn on debugging [default=no]],[if eval "test x$enable_debug = xyes"; then
        OPTIM="-g "
fi])

dnl Checks for programs...
AC_PROG_CC
AC_PATH_PROG(CHMOD,chmod)
AC_PATH_PROG(CP,cp)
AC_PATH_PROG(MKDIR,mkdir)

dnl Check for libraries...
AC_CHECK_LIB(socket,socket,
if test "$uname" != "IRIX"; then
	LIBS="-lsocket $LIBS"
else
	echo "Not using -lsocket since you are running IRIX."
fi)
AC_CHECK_LIB(nsl,gethostbyaddr,
if test "$uname" != "IRIX"; then
	LIBS="-lnsl $LIBS"
else
	echo "Not using -lnsl since you are running IRIX."
fi)

AC_CHECK_LIB(m,pow)
AC_CHECK_LIB(z,gzgets)

dnl Require CUPS 1.1...
AC_CHECK_LIB(cups,cupsPrintFiles)
AC_CHECK_LIB(cupsimage,cupsRasterOpen)

dnl add warnings to compiler switches:
dnl do this last so messing with switches does not break tests
if test -n "$GCC"; then
    if test "x$OPTIM" = "x"; then
        CFLAGS="$CFLAGS -O2"
    else
        CFLAGS="$CFLAGS -g"
    fi

    CFLAGS="-Wall $CFLAGS"

    # See if GCC supports -fno-exceptions...
    echo "Testing if GCC supports -fno-exceptions..."
    OLDCFLAGS="$CFLAGS"
    CFLAGS="$CFLAGS -fno-exceptions"
    AC_TRY_COMPILE(,,
	echo "Congratulations - your GCC supports exceptions; disabling them for CUPS...",
	CFLAGS="$OLDCFLAGS"
	echo "GCC does not support exceptions...")
else
    if test "x$OPTIM" = "x"; then
	case `uname` in
	  IRIX*)
            CFLAGS="$CFLAGS -O3"
	    ;;
	  *)
            CFLAGS="$CFLAGS -O"
            ;;
	esac
    else
	CFLAGS="$CFLAGS -g"
    fi
fi

dnl Fix "prefix" variable if it hasn't been specified...
if test "$prefix" = "NONE"; then
	prefix="/"
fi

dnl Fix "exec_prefix" variable if it hasn't been specified...
if test "$exec_prefix" = "NONE"; then
	if test "$prefix" = "/"; then
		exec_prefix="/usr"
	else
		exec_prefix="$prefix"
	fi
fi

dnl Fix "datadir" variable if it hasn't been specified...
if test "$datadir" = "\${prefix}/share" -a "$prefix" = "/"; then
	datadir="/usr/share"
fi

dnl Fix "sysconfdir" variable if it hasn't been specified...
if test "$sysconfdir" = "\${prefix}/etc"; then
	if test "$prefix" = "/"; then
		sysconfdir="/etc"
	else
		sysconfdir="$prefix/etc"
	fi
fi

AC_DEFINE_UNQUOTED(CUPS_DATADIR, "$exec_prefix/share/cups")

dnl Output the makefile...
AC_OUTPUT(Makefile)

dnl
dnl End of configure script.
dnl
