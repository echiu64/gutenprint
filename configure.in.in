dnl $Id$
dnl Copyright (C) 2000 Roger Leigh
dnl 
dnl This program is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation; either version 2, or (at your option)
dnl any later version.
dnl 
dnl This program is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl 
dnl You should have received a copy of the GNU General Public License
dnl along with this program; if not, write to the Free Software
dnl Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.

dnl Process this file with autoconf to produce a configure script.

AC_INIT(src/main/print-dither.c)
AC_CONFIG_AUX_DIR(scripts)

dnl In the following, there are a the following variants
dnl of gimpprint cflags and libs variables
dnl
dnl GIMPPRINT_CFLAGS:  cflags for compiling libraries and example progs
dnl GIMPPRINT_LIBS:    libraries for linking example programs
dnl GIMPPRINT_DEPLIBS: libraries for linking libraries against
dnl gimpprint_cflags:  cflags to store in gimpprint-config
dnl gimpprint_libs:    libs to store in gimpprint-config

dnl Save this value here, since automake will set cflags later
cflags_set=${CFLAGS+set}

dnl we need to AC_DIVERT_PUSH/AC_DIVERT_POP these variable definitions so they
dnl are available for $ac_help expansion (don't we all *love* autoconf?)
AC_DIVERT_PUSH(AC_DIVERSION_NOTICE)dnl
dnl
dnl Making releases:
dnl   GIMPPRINT_MICRO_VERSION += 1;
dnl   GIMPPRINT_INTERFACE_AGE += 1;
dnl   GIMPPRINT_BINARY_AGE += 1;
dnl if any functions have been added, set GIMPPRINT_INTERFACE_AGE to 0.
dnl if backwards compatibility has been broken,
dnl set GIMPPRINT_BINARY_AGE and GIMPPRINT_INTERFACE_AGE to 0.
dnl
GIMPPRINT_MAJOR_VERSION=4
GIMPPRINT_MINOR_VERSION=1
GIMPPRINT_MICRO_VERSION=4
GIMPPRINT_CURRENT_INTERFACE=1
GIMPPRINT_INTERFACE_AGE=0
GIMPPRINT_BINARY_AGE=0
GIMPPRINT_VERSION=$GIMPPRINT_MAJOR_VERSION.$GIMPPRINT_MINOR_VERSION.$GIMPPRINT_MICRO_VERSION
dnl
AC_DIVERT_POP()dnl

AC_SUBST(GIMPPRINT_MAJOR_VERSION)
AC_SUBST(GIMPPRINT_MINOR_VERSION)
AC_SUBST(GIMPPRINT_MICRO_VERSION)
AC_SUBST(GIMPPRINT_CURRENT_INTERFACE)
AC_SUBST(GIMPPRINT_INTERFACE_AGE)
AC_SUBST(GIMPPRINT_BINARY_AGE)
AC_SUBST(GIMPPRINT_VERSION)
AC_DEFINE_UNQUOTED(GIMPPRINT_MAJOR_VERSION,${GIMPPRINT_MAJOR_VERSION})
AC_DEFINE_UNQUOTED(GIMPPRINT_MINOR_VERSION,${GIMPPRINT_MINOR_VERSION})
AC_DEFINE_UNQUOTED(GIMPPRINT_MICRO_VERSION,${GIMPPRINT_MICRO_VERSION})
AC_DEFINE_UNQUOTED(GIMPPRINT_INTERFACE_AGE,${GIMPPRINT_INTERFACE_AGE})
AC_DEFINE_UNQUOTED(GIMPPRINT_BINARY_AGE,${GIMPPRINT_BINARY_AGE})
AC_DEFINE_UNQUOTED(GIMPPRINT_VERSION,${GIMPPRINT_VERSION})

dnl libtool versioning
LT_RELEASE=$GIMPPRINT_MAJOR_VERSION.$GIMPPRINT_MINOR_VERSION
LT_CURRENT=$GIMPPRINT_CURRENT_INTERFACE
dnl LT_CURRENT=`expr $GIMPPRINT_MICRO_VERSION - $GIMPPRINT_INTERFACE_AGE`
LT_REVISION=$GIMPPRINT_INTERFACE_AGE
LT_AGE=$GIMPPRINT_BINARY_AGE
dnl LT_AGE=`expr $GIMPPRINT_BINARY_AGE - $GIMPPRINT_INTERFACE_AGE`
AC_SUBST(LT_RELEASE)
AC_SUBST(LT_CURRENT)
AC_SUBST(LT_REVISION)
AC_SUBST(LT_AGE)

dnl Save this value here, since automake will set cflags later.
cflags_set=${CFLAGS+set}

dnl Initialize automake stuff.
AM_INIT_AUTOMAKE(gimp-print, $GIMPPRINT_VERSION)

dnl Specify a configuration file.
AM_CONFIG_HEADER(config.h)

dnl Available languages (translations)
ALL_LINGUAS="en_GB en_TEST"

dnl Set up libtool scripts.
dnl Disable static library building to speed up the build
AC_DISABLE_STATIC
AM_PROG_LIBTOOL
AC_SUBST(LIBTOOL_DEPS)
dnl define HAVE_GNU_LD if GNU ld is present
if test "$ac_cv_prog_gnu_ld" = "yes" ; then
  AC_DEFINE(HAVE_GNU_LD)
fi

dnl Compiler options and definitions
CFLAGS="${CFLAGS:=}"
INCLUDES="${INCLUDES} -I\$(top_srcdir)/src/include"
OPTIM=""

dnl Defaults
dnl figure defaults, prior to $ac_help setup
AC_DIVERT_PUSH(AC_DIVERSION_NOTICE)dnl
BUILD_CUPS=no
BUILD_GIMP=yes
BUILD_GHOST=no
BUILD_ESCPUTIL=yes
BUILD_LIBGIMPPRINT=yes
BUILD_TEST=no
PLUG_IN_PATH="admin-bin"
AC_DIVERT_POP()dnl

AM_MAINTAINER_MODE
if test x${USE_MAINTAINER_MODE} = xyes ; then
  MAINTAINER_MODE=true
  BUILD_TEST=yes
  AC_DEFINE(HAVE_MAINTAINER_MODE)
fi

dnl Check canonical system/host (a kludge: replace me with something better).
AC_CANONICAL_HOST
if test x${host_os} = xlinux-gnu ; then
  OSTYPE="linux"
elif test "`echo ${host_os} | sed 's/^\(linux\).*$/\1/'`" = "linux" ; then
  OSTYPE="linux"
elif test "`echo ${host_os} | sed 's/^\(irix\).*$/\1/'`" = "irix" ; then
  OSTYPE="irix"
else
  OSTYPE="${host_os}"
fi
AC_DEFINE(OSTYPE,["${OSTYPE}"],[The operating system to build for])

dnl Command line options.
AC_MSG_CHECKING([whether to enable debugging])
AC_ARG_ENABLE(debug,
[  --enable-debug          turn on debugging [default=no]],
  [case "${enableval}" in
    yes) OPTIM="-g " ; AC_MSG_RESULT([yes]) ;;
    no) AC_MSG_RESULT([no]) ;;
    *) AC_MSG_RESULT([unknown])
       AC_MSG_ERROR([bad value ${enableval} for --enable-debug]) ;;
  esac],[AC_MSG_RESULT([no])])

AC_MSG_CHECKING([whether to build CUPS driver])
AC_ARG_WITH(cups,
[  --with-cups[=PREFIX]    build CUPS driver [default=${BUILD_CUPS}, default PREFIX=PREFIX]],
  [case "${withval}" in
    yes) BUILD_CUPS="yes" ; AC_MSG_RESULT([yes]) ; cups_prefix="/usr" ;;
    no) BUILD_CUPS="no" ; AC_MSG_RESULT([no]) ; cups_prefix="/usr" ;;
    *) if test -d ${withval} ; then
         BUILD_CUPS="yes"
	 AC_MSG_RESULT([yes (using specified prefix)])
         cups_prefix="${withval}"
       else
         AC_MSG_RESULT([unknown])
         AC_MSG_ERROR([${withval}: no such directory for --with-cups])
       fi ;;
  esac],[AC_MSG_RESULT([${BUILD_CUPS}]) ; cups_prefix="${prefix}"])

AC_MSG_CHECKING([whether to build GIMP plugin])
AC_ARG_WITH(gimp,
[  --with-gimp             build GIMP plugin [default=${BUILD_GIMP}]],
  [case "${withval}" in
    yes) BUILD_GIMP="yes" ; AC_MSG_RESULT([yes]) ;;
    no) BUILD_GIMP="no" ; AC_MSG_RESULT([no]) ;;
    *) AC_MSG_RESULT([unknown])
       AC_MSG_ERROR([bad value ${withval} for --with-gimp]) ;;
  esac],[AC_MSG_RESULT([${BUILD_GIMP}])])

AC_MSG_CHECKING([whether to build ghostscript driver])
AC_ARG_WITH(ghost,
[  --with-ghost            build ghostscript driver patch [default=${BUILD_GHOST}]],
  [case "${withval}" in
    yes) BUILD_GHOST="yes" ; AC_MSG_RESULT([yes]) ;;
    no) BUILD_GHOST="no" ; AC_MSG_RESULT([no]) ;;
    *) AC_MSG_RESULT([unknown])
       AC_MSG_ERROR([bad value ${withval} for --with-ghost]) ;;
  esac],[AC_MSG_RESULT([${BUILD_GHOST}])])

AC_MSG_CHECKING([whether to build escputil])
AC_ARG_ENABLE(escputil,
[  --enable-escputil       build escputil [default=${BUILD_ESCPUTIL}]],
  [case "${enableval}" in
    yes) BUILD_ESCPUTIL="yes" ; AC_MSG_RESULT([yes]) ;;
    no) BUILD_ESCPUTIL="no" ; AC_MSG_RESULT([no]) ;;
    *) AC_MSG_RESULT([unknown])
       AC_MSG_ERROR([bad value ${enableval} for --enable-escputil]) ;;
  esac],[AC_MSG_RESULT([${BUILD_ESCPUTIL}])])

AC_MSG_CHECKING([whether to build libgimpprint])
AC_ARG_ENABLE(libgimpprint,
[  --enable-libgimpprint   build libgimpprint [default=${BUILD_LIBGIMPPRINT}]],
  [case "${enableval}" in
    yes) BUILD_LIBGIMPPRINT="yes" ; AC_MSG_RESULT([yes]) ;;
    no) BUILD_LIBGIMPPRINT="no" ; AC_MSG_RESULT([no]) ;;
    *) AC_MSG_RESULT([unknown])
       AC_MSG_ERROR([bad value ${enableval} for --enable-libgimpprint]) ;;
  esac],[AC_MSG_RESULT([${BUILD_LIBGIMPPRINT}])])

AC_MSG_CHECKING([whether to build test programs])
AC_ARG_ENABLE(test,
[  --enable-test           build test programs [default=${BUILD_TEST}]],
  [case "${enableval}" in
    yes) BUILD_TEST="yes" ; AC_MSG_RESULT([yes]) ;;
    no) BUILD_TEST="no" ; AC_MSG_RESULT([no]) ;;
    *) AC_MSG_RESULT([unknown])
       AC_MSG_ERROR([bad value ${enableval} for --enable-test]) ;;
  esac],[AC_MSG_RESULT([${BUILD_TEST}])])

AC_MSG_CHECKING([if user install is enabled])
AC_ARG_ENABLE(user-install,
[  --enable-user-install   install GIMP plugin in home directory [default=no]],
  [case "${enableval}" in
    yes) PLUG_IN_PATH="bin" ; AC_MSG_RESULT([yes]) ;;
    no) PLUG_IN_PATH="admin-bin" ; AC_MSG_RESULT([no]) ;;
    *) AC_MSG_RESULT([unknown])
       AC_MSG_ERROR([bad value ${enableval} for --enable-user-install]) ;;
  esac],[PLUG_IN_PATH="admin-bin" ; AC_MSG_RESULT([no])])


dnl Honour aclocal flags.
ACLOCAL="$ACLOCAL $ACLOCAL_FLAGS"

dnl Checks for programs.
AC_ISC_POSIX
AC_PROG_AWK
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_YACC
AM_PROG_LEX
AC_PATH_PROG(BISON_PROG, bison)
AC_PATH_PROG(YACC_PROG, yacc)

AC_MSG_CHECKING(whether make is GNU Make)
STRIP_BEGIN=
STRIP_END=
if $ac_make --version 2>/dev/null | grep '^GNU Make ' >/dev/null ; then
  STRIP_BEGIN='$(strip $(STRIP_DUMMY)'
  STRIP_END=')'
  AC_MSG_RESULT(yes)
else
  AC_MSG_RESULT(no)
fi
dnl some Make 3.79 $(strip ) versions are broken and require an empty arg
STRIP_DUMMY=
AC_SUBST(STRIP_DUMMY)
AC_SUBST(STRIP_BEGIN)
AC_SUBST(STRIP_END)

dnl GNU gettext checks.
AM_GNU_GETTEXT
if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${ac_default_prefix}/${DATADIRNAME}/locale")
else
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${prefix}/${DATADIRNAME}/locale")
fi

dnl Compiler flags
if test -n "${GCC}" ; then
  # Only GCC supports -Wall and other GCC options...
  if test x${USE_MAINTAINER_MODE} = xtrue ; then
    MAINTAINER_CFLAGS='-Wall -Wcast-align -Wstrict-prototypes -DGIMP_PRINT_MAINT'
  else
    MAINTAINER_CFLAGS='-Wall -Wcast-align -Wstrict-prototypes'
  fi
  if test -z "${CFLAGS}" ; then
    if test x${OPTIM} = x ; then
      CFLAGS="${CFLAGS} -O2"
    else
      CFLAGS="${CFLAGS} -g"
    fi
  fi
  if test x${BUILD_CUPS} = xyes ; then
    # See if GCC supports -fno-exceptions...
      AC_MSG_CHECKING([if GCC supports -fno-exceptions])
      OLDCFLAGS="$CFLAGS"
      CFLAGS="$CFLAGS -fno-exceptions"
      AC_TRY_COMPILE(,,
        AC_MSG_RESULT([yes (disabling for CUPS)])
        CUPS_CFLAGS="-fno-exceptions",
        AC_MSG_RESULT([no]))
      CFLAGS="$OLDCFLAGS"
  fi
else
  if test x${USE_MAINTAINER_MODE} = xyes ; then
    MAINTAINER_CFLAGS='-DGIMP_PRINT_MAINT'
  else
    MAINTAINER_CFLAGS=''
  fi
  if test -z "${CFLAGS}" ; then
    if test x${OPTIM} = x ; then
      case ${OSTYPE} in
        irix) CFLAGS="${CFLAGS} -O3" ;;
        *)    CFLAGS="${CFLAGS} -O" ;;
      esac
    else
      CFLAGS="${CFLAGS} -g"
    fi
  fi
fi

dnl Checks for libraries.
AC_CHECK_LIB(readline, readline, HAVE_LIBREADLINE=true, HAVE_LIBREADLINE=false)
if test x${HAVE_LIBREADLINE} = xtrue ; then
  AC_DEFINE(HAVE_LIBREADLINE,1)
else
  AC_DEFINE(HAVE_LIBREADLINE,0)
fi

dnl CUPS checks
if test x${BUILD_CUPS} = xyes ; then
  AC_CHECK_LIB(socket,socket,
    if test x${OSTYPE} != xirix ; then
    CUPS_LIBS="${CUPS_LIBS} -lsocket"
  fi)
  AC_CHECK_LIB(nsl,gethostbyaddr,
    if test x${OSTYPE} != xirix ; then
    CUPS_LIBS="${CUPS_LIBS} -lnsl"
  fi)
  AC_CHECK_LIB(m,pow,
    CUPS_LIBS="${CUPS_LIBS} -lm")
  AC_CHECK_LIB(z,gzgets,
    HAVE_LIBZ=true, HAVE_LIBZ=false)
  if test x${HAVE_LIBZ} = xtrue ; then
    CUPS_LIBS="${CUPS_LIBS} -lz"
    AC_DEFINE(HAVE_LIBZ,1)
  fi
dnl Require CUPS 1.1...
  AC_CHECK_LIB(cups,cupsPrintFiles,
    CUPS_LIBS="${CUPS_LIBS} -lcups",
    AC_MSG_ERROR([Cannot find CUPS libraries (libcups)]))
  AC_CHECK_LIB(cupsimage,cupsRasterOpen,
    CUPS_LIBS="${CUPS_LIBS} -lcupsimage",
    AC_MSG_ERROR([Cannot find CUPS libraries (libcupsimage)]),
    -lcups)

fi

dnl GIMP checks
if test x${BUILD_GIMP} = xyes -o -n "${BUILD_PACKAGE}" ; then
  AM_PATH_GIMP(1.0.0,
    [SAVE_GTK_LIBS="$GIMP_LIBS"
    SAVE_GTK_CFLAGS="$GIMP_CFLAGS"],
    AC_MSG_ERROR(Cannot find GIMP libraries: Please run ldconfig as root, and make sure gimptool is on your PATH.))
fi

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(unistd.h)
AC_CHECK_HEADER(readline/readline.h,
  HAVE_READLINE_READLINE_H=true,
  HAVE_READLINE_READLINE_H=false)
if test x${HAVE_READLINE_READLINE_H} = xtrue ; then
  AC_DEFINE(HAVE_READLINE_READLINE_H)
  if test x${HAVE_LIBREADLINE} = xtrue ; then
    LIBREADLINE_DEPS="-lreadline"
  fi
fi

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_OFF_T
AC_TYPE_SIZE_T
AC_TYPE_SIGNAL

dnl Checks for library functions.
AC_FUNC_ALLOCA
dnl [commented out while I find a memcmp.c for lib/] AC_FUNC_MEMCMP
dnl GNU libc memcmp.c is too glibc-dependent to include.
AC_CHECK_FUNC(random, AC_DEFINE(HAVE_RANDOM))
AC_CHECK_FUNC(poll, AC_DEFINE(HAVE_POLL))
dnl The following is used to build a "utility library" in lib/
AC_REPLACE_FUNCS(vasprintf xgetcwd xmalloc)
AC_CHECK_FUNC(asprintf,
  AC_DEFINE(HAVE_ASPRINTF),
  LIBOBJS="${LIBOBJS} vasprintf.o")
AC_CHECK_FUNC(getopt_long,
  AC_DEFINE(HAVE_GETOPT_LONG),
  LIBOBJS="${LIBOBJS} getopt.o getopt1.o")

dnl Define what has to be built
if test x${BUILD_CUPS} = xyes ; then
  CUPS_BIN="cups-calibrate"
  CUPS_BACKEND_BIN="epson canon"
  CUPS_FILTER_BIN="rastertoprinter commandtoepson commandtocanon"
  CUPS_NOINST_BIN="genppd"
  CUPS_PKG="calibrate.ppm"
  CUPS_CONF="command.types"
  CUPS_INSTALL="install-am"
else
  CUPS_INSTALL=""
fi
AM_CONDITIONAL(BUILD_CUPS, test x${BUILD_CUPS} = xyes)
if test x${BUILD_GIMP} = xyes ; then
  GIMP_BIN="print"
fi
if test x${BUILD_GHOST} = xyes ; then
  GHOST_BIN="true"
fi
AM_CONDITIONAL(GHOST_BIN, test x${BUILD_GHOST} = xyes)
if test x${BUILD_ESCPUTIL} = xyes ; then
  ESCPUTIL_BIN="escputil"
fi
AM_CONDITIONAL(BUILD_ESCPUTIL, test x${BUILD_ESCPUTIL} = xyes)
if test x${BUILD_LIBGIMPPRINT} = xyes ; then
  MAIN_BIN="libgimpprint.la"
  GIMPPRINT_LIBS="\$(top_builddir)/src/main/libgimpprint.la"
  GIMP_PRINT_H="gimp-print.h"
fi
if test x${BUILD_TEST} = xyes ; then
  TEST_BIN="escp2-weavetest printer_options unprint escp2-unprint pcl-unprint"
fi

dnl CUPS path setup
dnl Fix "prefix" variable if it hasn't been specified...
if test x${cups_prefix} = xNONE ; then
  cups_prefix="/usr"
fi
dnl Fix "exec_prefix" variable if it hasn't been specified...
if test x${exec_prefix} = xNONE ; then
  if test "${cups_prefix}" = "/" ; then
    cups_exec_prefix="/usr"
  else
    cups_exec_prefix="${cups_prefix}"
  fi
fi
dnl Fix "bindir" variable if it hasn't been specified...
if test "${datadir}" = "\${prefix}/share" -a "${cups_prefix}" = "/" ; then
  cups_bindir="/usr/bin"
else
  cups_bindir="${cups_prefix}/bin"
fi
dnl Fix "datadir" variable if it hasn't been specified...
if test "${datadir}" = "\${prefix}/share" -a "${cups_prefix}" = "/" ; then
  cups_datadir="/usr/share"
else
  cups_datadir="${cups_prefix}/share"
fi
dnl Fix "libdir" variable if it hasn't been specified...
if test "${libdir}" = "\${prefix}/lib" -a "${cups_prefix}" = "/" ; then
  cups_libdir="/usr/lib"
else
  cups_libdir="${cups_prefix}/lib"
fi
dnl Fix "sysconfdir" variable if it hasn't been specified...
if test "${sysconfdir}" = "\${prefix}/etc"; then
  if test "${cups_prefix}" = "/usr" ; then
    cups_sysconfdir="/etc"
  else
    cups_sysconfdir="${cups_prefix}/etc"
  fi
fi

dnl Define LTLIBOBJS
LTLIBOBJS=`echo "$LIBOBJS" | sed 's/\.o/.lo/g'`
LTALLOCA=`echo "$ALLOCA" | sed 's/\.o/.lo/g'`

dnl Definitions
AC_DEFINE_UNQUOTED(CUPS_DATADIR, "${cups_exec_prefix}/share/cups")
XXXRELEASE_DATE=XXX
AC_DEFINE_UNQUOTED(RELEASE_DATE, "${RELEASE_DATE}")

dnl Substitutions
AC_SUBST(cups_prefix)
AC_SUBST(cups_exec_prefix)
AC_SUBST(cups_bindir)
AC_SUBST(cups_datadir)
AC_SUBST(cups_libdir)
AC_SUBST(cups_sysconfdir)
AC_SUBST(CUPS_BIN)
AC_SUBST(CUPS_BACKEND_BIN)
AC_SUBST(CUPS_FILTER_BIN)
AC_SUBST(CUPS_CFLAGS)
AC_SUBST(CUPS_PKG)
AC_SUBST(CUPS_CONF)
AC_SUBST(CUPS_INSTALL)
AC_SUBST(CUPS_LIBS)
AC_SUBST(CUPS_NOINST_BIN)
AC_SUBST(ESCPUTIL_BIN)
AC_SUBST(GIMP_BIN)
AC_SUBST(GIMP_LIBS)
AC_SUBST(GIMP_PRINT_H)
AC_SUBST(GIMPPRINT_LIBS)
AC_SUBST(INCLUDES)
AC_SUBST(LIBREADLINE_DEPS)
AC_SUBST(LTLIBOBJS)
AC_SUBST(LTALLOCA)
AC_SUBST(MAIN_BIN)
AC_SUBST(MAINTAINER_CFLAGS)
AC_SUBST(PLUG_IN_PATH)
AC_SUBST(RELEASE_DATE)
AC_SUBST(TEST_BIN)

dnl Output files
AC_OUTPUT([
Makefile
intl/Makefile
lib/Makefile
man/Makefile
man/cups-calibrate.8
man/escputil.1
man/gimpprint-config.1
po/Makefile.in
src/Makefile
src/cups/Makefile
src/escputil/Makefile
src/ghost/Makefile
src/gimp/Makefile
src/include/Makefile
src/main/Makefile
src/main/gimpprint-config
src/printdef/Makefile
test/Makefile
],[
chmod +x src/main/gimpprint-config
echo "Finished configuring."
echo "Type 'make' to build the package"
echo "then 'make install' to install it."
])
