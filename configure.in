dnl $Id$
dnl Copyright (C) 2000 Roger Leigh
dnl 
dnl This program is free software; you can redistribute it and/or modify
dnl it under the terms of the GNU General Public License as published by
dnl the Free Software Foundation; either version 2, or (at your option)
dnl any later version.
dnl 
dnl This program is distributed in the hope that it will be useful,
dnl but WITHOUT ANY WARRANTY; without even the implied warranty of
dnl MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
dnl GNU General Public License for more details.
dnl 
dnl You should have received a copy of the GNU General Public License
dnl along with this program; if not, write to the Free Software
dnl Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA 02111-1307, USA.

dnl Process this file with autoconf to produce a configure script.

AC_INIT(src/main/print-dither.c)
AC_CONFIG_AUX_DIR(scripts)
AM_INIT_AUTOMAKE(gimp-print,4.1.2)
AM_CONFIG_HEADER(config.h)

dnl Available languages (translations)
ALL_LINGUAS=""

dnl Compiler options and definitions
CFLAGS="${CFLAGS:=}"
INCLUDES="${INCLUDES} -I\$(top_srcdir)/src/include"
OPTIM=""
RELDATE="\"`date '+%d %b %Y'`\""
RELDATE_DEF="-DRELEASE_DATE=\"\\\"`date '+%d %b %Y'`\\\"\""

dnl Defaults
BUILD_CUPS=no
BUILD_GIMP=yes
BUILD_GHOST=no
BUILD_ESCPUTIL=yes
BUILD_LIBGIMPPRINT=yes
BUILD_TEST=no
PLUG_IN_PATH="admin-bin"

AM_MAINTAINER_MODE
if test x${USE_MAINTAINER_MODE} = xyes ; then
  MAINTAINER_MODE=true
  BUILD_TEST=yes
else
  BUILD_TEST=''
fi

dnl Check canonical system/host
dnl (this is a kludge; replace with AC_PATH_XTRA
dnl and a new m4 macro)
AC_CANONICAL_HOST
if test x${host_os} = xlinux-gnu ; then
  OSTYPE="linux"
elif test "`echo ${host_os} | sed 's/^\(linux\).*$/\1/'`" = "linux" ; then
  OSTYPE="linux"
elif test "`echo ${host_os} | sed 's/^\(irix\).*$/\1/'`" = "irix" ; then
  OSTYPE="irix"
else
  OSTYPE="${host_os}"
fi
AC_DEFINE(OSTYPE,["${OSTYPE}"],[The operating system to build for])

dnl Command line options
AC_MSG_CHECKING([whether to enable debugging])
AC_ARG_ENABLE(debug,
[  --enable-debug          turn on debugging [default=no]],
  [case "${enableval}" in
    yes) OPTIM="-g " ; AC_MSG_RESULT([yes]) ;;
    no) AC_MSG_RESULT([no]) ;;
    *) AC_MSG_RESULT([unknown])
       AC_MSG_ERROR([bad value ${enableval} for --enable-debug]) ;;
  esac],[AC_MSG_RESULT([no])])

AC_MSG_CHECKING([whether to build CUPS driver])
AC_ARG_WITH(cups,
[  --with-cups[=PREFIX]    build CUPS driver [default=no]],
  [case "${withval}" in
    yes) BUILD_CUPS="yes" ; AC_MSG_RESULT([yes]) ; cups_prefix="${prefix}" ;;
    no) BUILD_CUPS="no" ; AC_MSG_RESULT([no]) ; cups_prefix="${prefix}" ;;
    *) if test -d ${withval} ; then
         AC_MSG_RESULT([yes (using specified prefix)])
         cups_prefix="${withval}"
       else
         AC_MSG_RESULT([unknown])
         AC_MSG_ERROR([bad value ${withval} for --with-cups])
       fi ;;
  esac],[AC_MSG_RESULT([${BUILD_CUPS} ; cups_prefix="/usr"])])

AC_MSG_CHECKING([whether to build GIMP plugin])
AC_ARG_WITH(gimp,
[  --with-gimp             build GIMP plugin [default=yes]],
  [case "${withval}" in
    yes) BUILD_GIMP="yes" ; AC_MSG_RESULT([yes]) ;;
    no) BUILD_GIMP="no" ; AC_MSG_RESULT([no]) ;;
    *) AC_MSG_RESULT([unknown])
       AC_MSG_ERROR([bad value ${withval} for --with-gimp]) ;;
  esac],[AC_MSG_RESULT([${BUILD_GIMP}])])

AC_MSG_CHECKING([whether to build ghostscript driver])
AC_ARG_WITH(ghost,
[  --with-ghost            build ghostscript driver patch [default=no]],
  [case "${withval}" in
    yes) BUILD_GHOST="yes" ; AC_MSG_RESULT([yes]) ;;
    no) BUILD_GHOST="no" ; AC_MSG_RESULT([no]) ;;
    *) AC_MSG_RESULT([unknown])
       AC_MSG_ERROR([bad value ${withval} for --with-ghost]) ;;
  esac],[AC_MSG_RESULT([${BUILD_GHOST}])])

AC_MSG_CHECKING([whether to build escputil])
AC_ARG_ENABLE(escputil,
[  --enable-escputil       build escputil [default=yes]],
  [case "${enableval}" in
    yes) BUILD_ESCPUTIL="yes" ; AC_MSG_RESULT([yes]) ;;
    no) BUILD_ESCPUTIL="no" ; AC_MSG_RESULT([no]) ;;
    *) AC_MSG_RESULT([unknown])
       AC_MSG_ERROR([bad value ${enableval} for --enable-escputil]) ;;
  esac],[AC_MSG_RESULT([${BUILD_ESCPUTIL}])])

AC_MSG_CHECKING([whether to build libgimpprint])
AC_ARG_ENABLE(libgimpprint,
[  --enable-libgimpprint   build libgimpprint [default=yes]],
  [case "${enableval}" in
    yes) BUILD_LIBGIMPPRINT="yes" ; AC_MSG_RESULT([yes]) ;;
    no) BUILD_LIBGIMPPRINT="no" ; AC_MSG_RESULT([no]) ;;
    *) AC_MSG_RESULT([unknown])
       AC_MSG_ERROR([bad value ${enableval} for --enable-libgimpprint]) ;;
  esac],[AC_MSG_RESULT([${BUILD_LIBGIMPPRINT}])])

AC_MSG_CHECKING([whether to build test programs])
AC_ARG_ENABLE(test,
[  --enable-test           build test programs [default=no]],
  [case "${enableval}" in
    yes) BUILD_TEST="yes" ; AC_MSG_RESULT([yes]) ;;
    no) BUILD_TEST="no" ; AC_MSG_RESULT([no]) ;;
    *) AC_MSG_RESULT([unknown])
       AC_MSG_ERROR([bad value ${enableval} for --enable-test]) ;;
  esac],[AC_MSG_RESULT([${BUILD_TEST}])])

AC_MSG_CHECKING([if user install is enabled])
AC_ARG_ENABLE(user-install,
[  --enable-user-install   install GIMP plugin in home directory [default=no]],
  [case "${enableval}" in
    yes) PLUG_IN_PATH="bin" ; AC_MSG_RESULT([yes]) ;;
    no) PLUG_IN_PATH="admin-bin" ; AC_MSG_RESULT([no]) ;;
    *) AC_MSG_RESULT([unknown])
       AC_MSG_ERROR([bad value ${enableval} for --enable-user-install]) ;;
  esac],[PLUG_IN_PATH="admin-bin" ; AC_MSG_RESULT([no])])

dnl Set up libtool scripts.
dnl Disable static library building to speed up the build
AC_DISABLE_STATIC
AM_PROG_LIBTOOL
AC_SUBST(LIBTOOL_DEPS)
dnl define HAVE_GNU_LD if GNU ld is present
if test "$ac_cv_prog_gnu_ld" = "yes" ; then
  AC_DEFINE(HAVE_GNU_LD)
fi

dnl Checks for programs.
AC_ISC_POSIX
AC_PROG_AWK
AC_PROG_CC
AC_PROG_INSTALL
AC_PROG_LN_S
AC_PROG_YACC
AM_PROG_LEX
AC_PATH_PROG(BISON_PROG, bison)
AC_PATH_PROG(YACC_PROG, yacc)

dnl GNU gettext checks.
AM_GNU_GETTEXT
if test "x${prefix}" = "xNONE"; then
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${ac_default_prefix}/${DATADIRNAME}/locale")
else
  AC_DEFINE_UNQUOTED(PACKAGE_LOCALE_DIR, "${prefix}/${DATADIRNAME}/locale")
fi

dnl Compiler flags
if test -n "${GCC}" ; then
  # Only GCC supports -Wall and other GCC options...
  if test x${USE_MAINTAINER_MODE} = xtrue ; then
    MAINTAINER_CFLAGS='-Wall -Wcast-align -Wstrict-prototypes -DGIMP_PRINT_MAINT'
  else
    MAINTAINER_CFLAGS='-Wall -Wcast-align -Wstrict-prototypes'
  fi
  if test -z "${CFLAGS}" ; then
    if test x${OPTIM} = x ; then
      CFLAGS="${CFLAGS} -O2"
    else
      CFLAGS="${CFLAGS} -g"
    fi
  fi
  if test x${BUILD_CUPS} = xyes ; then
    # See if GCC supports -fno-exceptions...
      AC_MSG_CHECKING([if GCC supports -fno-exceptions])
      OLDCFLAGS="$CFLAGS"
      CFLAGS="$CFLAGS -fno-exceptions"
      AC_TRY_COMPILE(,,
        AC_MSG_RESULT([yes (disabling for CUPS)])
        CUPS_CFLAGS="-fno-exceptions",
        AC_MSG_RESULT([no]))
      CFLAGS="$OLDCFLAGS"
  fi
else
  if test x${USE_MAINTAINER_MODE} = xyes ; then
    MAINTAINER_CFLAGS='-DGIMP_PRINT_MAINT'
  else
    MAINTAINER_CFLAGS=''
  fi
  if test -z "${CFLAGS}" ; then
    if test x${OPTIM} = x ; then
      case ${OSTYPE} in
        irix) CFLAGS="${CFLAGS} -O3" ;;
        *)    CFLAGS="${CFLAGS} -O" ;;
      esac
    else
      CFLAGS="${CFLAGS} -g"
    fi
  fi
fi

dnl Checks for libraries.
AC_CHECK_LIB(readline, readline, HAVE_LIBREADLINE=true, HAVE_LIBREADLINE=false)
if test x${HAVE_LIBREADLINE} = xtrue ; then
  AC_DEFINE(HAVE_LIBREADLINE,1)
else
  AC_DEFINE(HAVE_LIBREADLINE,0)
fi

dnl CUPS checks
if test x${BUILD_CUPS} = xyes ; then
  AC_CHECK_LIB(socket,socket,
    if test x${OSTYPE} != xirix ; then
    CUPS_LIBS="${CUPS_LIBS} -lsocket"
  fi)
  AC_CHECK_LIB(nsl,gethostbyaddr,
    if test x${OSTYPE} != xirix ; then
    CUPS_LIBS="${CUPS_LIBS} -lnsl"
  fi)
  AC_CHECK_LIB(m,pow,
    CUPS_LIBS="${CUPS_LIBS} -lm")
  AC_CHECK_LIB(z,gzgets,
    CUPS_LIBS="${CUPS_LIBS} -lz")
dnl Require CUPS 1.1...
  AC_CHECK_LIB(cups,cupsPrintFiles,
    CUPS_LIBS="${CUPS_LIBS} -lcups")
  AC_CHECK_LIB(cupsimage,cupsRasterOpen,
    CUPS_LIBS="${CUPS_LIBS} -lcupsimage",,
    -lcups)
fi

dnl GIMP checks
if test x${BUILD_GIMP} = xyes ; then
  AM_PATH_GIMP(1.0.0,
    [SAVE_GTK_LIBS="$GIMP_LIBS"
    SAVE_GTK_CFLAGS="$GIMP_CFLAGS"],
    AC_MSG_ERROR(Cannot find GIMP libraries: Please run ldconfig as root, and make sure gimptool is on your PATH.))
fi

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(unistd.h)
AC_CHECK_HEADER(readline/readline.h,
  HAVE_READLINE_READLINE_H=true,
  HAVE_READLINE_READLINE_H=false)
if test x${HAVE_READLINE_READLINE_H} = xtrue ; then
  AC_DEFINE(HAVE_READLINE_READLINE_H)
  if test x${HAVE_LIBREADLINE} = xtrue ; then
    LIBREADLINE_DEPS="-lreadline"
  fi
fi

dnl Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_OFF_T
AC_TYPE_SIZE_T
AC_TYPE_SIGNAL

dnl Checks for library functions.
AC_FUNC_ALLOCA
dnl [commented out while I find a memcmp.c for lib/] AC_FUNC_MEMCMP
dnl GNU libc memcmp.c is too glibc-dependent to include.
AC_CHECK_FUNC(random, AC_DEFINE(HAVE_RANDOM))
dnl The following is used to build a "utility library" in lib/
AC_REPLACE_FUNCS(vasprintf xgetcwd xmalloc)
AC_CHECK_FUNC(asprintf,
  AC_DEFINE(HAVE_ASPRINTF),
  LIBOBJS="${LIBOBJS} vasprintf.o")
AC_CHECK_FUNC(getopt_long,
  AC_DEFINE(HAVE_GETOPT_LONG),
  LIBOBJS="${LIBOBJS} getopt.o getopt1.o")

dnl Define what has to be built
if test x${BUILD_CUPS} = xyes ; then
  CUPS_BIN="calibrate"
  CUPS_NOINST_BIN="canon commandtocanon commandtoepson epson genppd ppd rastertoprinter"
fi
if test x${BUILD_GIMP} = xyes ; then
  GIMP_BIN="print"
fi
if test x${BUILD_GHOST} = xyes ; then
  GHOST_BIN="true"
fi
AM_CONDITIONAL(GHOST_BIN, test x${BUILD_GHOST} = xyes)
if test x${BUILD_ESCPUTIL} = xyes ; then
  ESCPUTIL_BIN="escputil"
fi
if test x${BUILD_LIBGIMPPRINT} = xyes ; then
  MAIN_BIN="libgimpprint.la"
  GIMPPRINT_LIBS="\$(top_srcdir)/src/main/libgimpprint.la"
fi
if test x${BUILD_TEST} = xyes ; then
  TEST_BIN="escp2-weavetest printer_options unprint escp2-unprint pcl-unprint"
fi

dnl CUPS path setup
dnl Fix "prefix" variable if it hasn't been specified...
if test x${cups_prefix} = xNONE ; then
  cups_prefix="/"
fi
dnl Fix "exec_prefix" variable if it hasn't been specified...
if test x${exec_prefix} = xNONE ; then
  if test "${cups_prefix}" = "/" ; then
    cups_exec_prefix="/usr"
  else
    cups_exec_prefix="${cups_prefix}"
  fi
fi
dnl Fix "datadir" variable if it hasn't been specified...
if test "${datadir}" = "\${prefix}/share" -a "${cups_prefix}" = "/" ; then
  cups_datadir="/usr/share"
else
  cups_datadir="${datadir}"
fi
dnl Fix "sysconfdir" variable if it hasn't been specified...
if test "${sysconfdir}" = "\${prefix}/etc"; then
  if test "${cups_prefix}" = "/" ; then
    cups_sysconfdir="/etc"
  else
    cups_sysconfdir="${cups_prefix}/etc"
  fi
fi

dnl Definitions
AC_DEFINE_UNQUOTED(CUPS_DATADIR, "${cups_exec_prefix}/share/cups")

dnl Substitutions
AC_SUBST(cups_prefix)
AC_SUBST(cups_exec_prefix)
AC_SUBST(cups_datadir)
AC_SUBST(cups_sysconfdir)
AC_SUBST(CUPS_BIN)
AC_SUBST(CUPS_CFLAGS)
AC_SUBST(CUPS_LIBS)
AC_SUBST(CUPS_NOINST_BIN)
AC_SUBST(ESCPUTIL_BIN)
AC_SUBST(GIMP_BIN)
AC_SUBST(GIMP_LIBS)
AC_SUBST(GIMPPRINT_LIBS)
AC_SUBST(INCLUDES)
AC_SUBST(LIBREADLINE_DEPS)
AC_SUBST(MAIN_BIN)
AC_SUBST(MAINTAINER_CFLAGS)
AC_SUBST(PLUG_IN_PATH)
AC_SUBST(RELDATE)
AC_SUBST(RELDATE_DEF)
AC_SUBST(TEST_BIN)

dnl Output files
AC_OUTPUT([
Makefile
intl/Makefile
lib/Makefile
man/Makefile
man/escputil.1
po/Makefile.in
src/Makefile
src/cups/Makefile
src/escputil/Makefile
src/ghost/Makefile
src/gimp/Makefile
src/include/Makefile
src/main/Makefile
src/printdef/Makefile
test/Makefile
],[
echo "Finished configuring."
echo "Type 'make' to build the package"
echo "then 'make install' to install it."
])
